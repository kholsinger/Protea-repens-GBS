model {
   ## genotype frequencies
   ##
   for (i in 1:n.pops) {
     for (j in 1:n.loci) {
       x[i,j,1] <- p[i,j]*p[i,j] + f[i,j]*p[i,j]*(1-p[i,j])
       x[i,j,2] <- 2*(1-f[i,j])*p[i,j]*(1 - p[i,j])
       x[i,j,3] <- (1-p[i,j])*(1-p[i,j]) + f[i,j]*p[i,j]*(1-p[i,j])
     }
   }

   ## likelihood
   ##
   for (i in 1:n.pops) {
     for (j in 1:n.loci) {
       n[i,j,1:3] ~ dmulti(x[i,j,], N[i,j])
     }
   }

   ## priors
   ##
   for (i in 1:n.pops) {
     for (j in 1:n.loci) {
       p[i,j] ~ dbeta(alpha[i,j], beta[i,j])
       f[i,j] <- w[i,j]*(1 - f.min[i,j]) + f.min[i,j]
       f.min[i,j] <- max(-p[i,j]/(1-p[i,j]), -(1-p[i,j])/p[i,j])
       w[i,j] ~ dunif(0, 1)
     }
   }
   ## population effect on theta[i,j]
   ##
   for (i in 1:n.pops) {
     theta.pop[i] ~ dunif(0, 1)
   }
   ## locus effect on theta[i,j]
   ##
   for (j in 1:n.loci) {
     theta.locus[j] ~ dunif(0, 1)
   }
   ## parameters for allele frequency distribution
   ##
   for (i in 1:n.pops) {
     for (j in 1:n.loci) {
       theta[i,j] <- 1 - (1 - theta.pop[i])*(1 - theta.locus[j])
       alpha[i,j] <- ((1-theta[i,j])/theta[i,j])*pi[j]
       beta[i,j]  <- ((1-theta[i,j])/theta[i,j])*(1-pi[j])
     }
   }
   for (j in 1:n.loci) {
     pi[j] ~ dunif(0, 1)
   }
}


