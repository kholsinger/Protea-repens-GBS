model {
   ## genotype frequencies
   ##
   for (i in 1:n.pops) {
     for (j in 1:n.loci) {
       x[i,j,1] <- p[i,j]*p[i,j] + f*p[i,j]*(1-p[i,j])
       x[i,j,2] <- 2*(1-f)*p[i,j]*(1 - p[i,j])
       x[i,j,3] <- (1-p[i,j])*(1-p[i,j]) + f*p[i,j]*(1-p[i,j])
     }
   }

   ## likelihood
   ##
   for (i in 1:n.pops) {
     for (j in 1:n.loci) {
       n[i,j,1:3] ~ dmulti(x[i,j,], N[i,j])
     }
   }

   ## priors
   ##
   for (i in 1:n.pops) {
     for (j in 1:n.loci) {
       p[i,j] ~ dbeta(alpha[i,j], beta[i,j])
     }
   }
   f ~ dunif(0, 1)
   ## population effect on theta[i,j]
   ##
   for (i in 1:n.pops) {
     theta.pop[i] ~ dbeta(alpha.pop, beta.pop)
   }
   alpha.pop <- max(1, min(((1-theta.p)/theta.p)*pi.p, 1.0e4))
   beta.pop  <- max(1, min(((1-theta.p)/theta.p)*(1-pi.p), 1.0e4))
   theta.p    ~ dbeta(nu.p.t, omega.p.t)
   pi.p       ~ dbeta(nu.p.pi, omega.p.pi)
   ## locus effect on theta[i,j]
   ##
   for (j in 1:n.loci) {
     theta.locus[j] ~ dbeta(alpha.locus, beta.locus)
   }
   alpha.locus <- max(1, min(((1-theta.l)/theta.l)*pi.l, 1.0e4))
   beta.locus  <- max(1, min(((1-theta.l)/theta.l)*(1-pi.l), 1.0e4))
   theta.l      ~ dbeta(nu.l.t, omega.l.t)
   pi.l         ~ dbeta(nu.l.pi, omega.l.pi)
   ## parameters for allele frequency distribution
   ##
   for (i in 1:n.pops) {
     for (j in 1:n.loci) {
       theta[i,j] <- 1 - (1 - theta.pop[i])*(1 - theta.locus[j])
       alpha[i,j] <- max(1, min(((1-theta[i,j])/theta[i,j])*pi[j], 1.0e4))
       beta[i,j]  <- max(1, min(((1-theta[i,j])/theta[i,j])*(1-pi[j]), 1.0e4))
     }
   }
   for (j in 1:n.loci) {
     pi[j] ~ dunif(0, 1)
   }
}


